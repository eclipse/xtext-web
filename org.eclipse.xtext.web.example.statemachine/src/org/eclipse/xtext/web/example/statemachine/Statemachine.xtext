/*******************************************************************************
 * Copyright (c) 2015 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
grammar org.eclipse.xtext.web.example.statemachine.Statemachine with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate statemachine "http://www.eclipse.org/xtext/web/example/Statemachine"

Statemachine :
	{Statemachine}
	signals+=Signal*
	states+=State*;

Signal:
	(InputSignal | OutputSignal) 'signal' name=ID;

InputSignal:
	{InputSignal}
	'input';

OutputSignal:
	{OutputSignal}
	'output';

State:
	'state' name=ID
		commands+=Command*
		transitions+=Transition*
		nestedStates+=State*
	'end';

Transition:
	'if' condition=Condition 'goto' state=[State|QualifiedName];

Condition:
	events+=Event ('and' events+=Event)*;

Event:
	signal=[Signal] '==' value=BOOLEAN;

Command:
	'set' signal=[Signal] '=' newValue=BOOLEAN;

QualifiedName:
	ID ('.' ID)*;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';
